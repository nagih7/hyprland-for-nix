pragma ComponentBehavior: Bound
import qs
import qs.services
import qs.modules.common
import qs.modules.common.functions
import qs.modules.common.panels.lock
import QtQuick
import Quickshell
import Quickshell.Hyprland

LockScreen {
    id: root

    // Monitor name -> workspace id to restore on unlock (set when locking)
    property var savedWorkspaces: ({})

    Timer {
        id: restoreTimer
        interval: 150
        repeat: false
        onTriggered: {
            // Hyprland 0.55 Lua mode: --batch dispatch doesn't work; use eval
            // to run multiple dispatches atomically in a single Lua expression.
            var evalCmd = ""
            for (var j = 0; j < Quickshell.screens.length; ++j) {
                var monName = Quickshell.screens[j].name
                var wsId = root.savedWorkspaces[monName]
                if (wsId !== undefined) {
                    evalCmd += "hl.dispatch(hl.dsp.focus({monitor='" + monName + "'})); "
                    evalCmd += "hl.dispatch(hl.dsp.focus({workspace='" + wsId + "'})); "
                }
            }
            if (evalCmd.length > 0) {
                Quickshell.execDetached(["hyprctl", "eval", evalCmd])
                Quickshell.execDetached(["hyprctl", "reload"])
            }
        }
    }

    lockSurface: LockSurface {
        context: root.context
    }

    // Single batch for lock and unlock so we don't race multiple hyprctl calls
    Connections {
        target: GlobalStates
        function onScreenLockedChanged() {
            if (GlobalStates.screenLocked) {
                // Lock: save workspace per monitor and move all to temp workspace in one batch
                var next = {}
                // Lua mode: set lock animation then dispatch all monitors/workspaces atomically
                var evalCmd = "hl.animation({leaf='workspaces', enabled=true, speed=1, bezier='menu_decel', style='slidevert'}); "
                for (var i = 0; i < Quickshell.screens.length; ++i) {
                    var mon = Quickshell.screens[i].name
                    var mData = HyprlandData.monitors.find(m => m.name === mon)
                    if (mData?.activeWorkspace == undefined) {
                        return;
                    }
                    var ws = (mData?.activeWorkspace?.id ?? 1)
                    next[mon] = ws
                    evalCmd += "hl.dispatch(hl.dsp.focus({monitor='" + mon + "'})); "
                    evalCmd += "hl.dispatch(hl.dsp.focus({workspace='" + (2147483647 - ws) + "'})); "
                }
                root.savedWorkspaces = next
                Quickshell.execDetached(["hyprctl", "eval", evalCmd])
                Quickshell.execDetached(["hyprctl", "reload"])
            } else {
                restoreTimer.start()
            }
        }
    }

    // Push everything down (visual only; workspace switch is in Connections above)
    Variants {
        model: Quickshell.screens
        delegate: Scope {
            required property ShellScreen modelData
            property bool shouldPush: GlobalStates.screenLocked
            property string targetMonitorName: modelData.name
            property int verticalMovementDistance: modelData.height
            property int horizontalSqueeze: modelData.width * 0.2
        }
    }
}
